plugins {
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"    // (1)
	id 'java'
}


repositories {
	mavenCentral()
}
// (2)
ext {
	set('snippetsDir', file("build/generated-snippets"))
}
// (3)
configurations {
	asciidoctorExtensions
}

dependencies {
	// (4) mockmvc 라이브러리
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	// (5) 아스키 닥터
	asciidoctorExtensions "org.springframework.restdocs:spring-restdocs-asciidoctor"

	//스프링데이터 JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'



}

// (6)
tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

// (7)
tasks.named('asciidoctor') {
	configurations "asciidoctorExtensions"
	inputs.dir snippetsDir
	dependsOn test
}

// (8)
task copyDocument(type: Copy) {
	dependsOn asciidoctor            // (8-1)
	from file("${asciidoctor.outputDir}")   // (8-2)
	into file("src/main/resources/static/docs")   // (8-3)
}

build {
	dependsOn copyDocument  // (9)
}

// (10)
bootJar {
	dependsOn copyDocument    // (10-1)
	from("${asciidoctor.outputDir}") {  // (10-2)
		into 'static/docs'     // (10-3)
	}
}
